/*
 * Copyright (c) 2000-2021 Oracle and/or its affiliates.
 *
 * Licensed under the Universal Permissive License v 1.0 as shown at
 * http://oss.oracle.com/licenses/upl.
 */

plugins {
    id "application"
}
apply plugin: "java"


group = 'com.oracle.coherence.tutorials'
version = '1.0.0-SNAPSHOT'
sourceCompatibility = '1.8'

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    implementation platform("${coherenceGroupId}:coherence-bom:${coherenceVersion}")
    implementation platform("${coherenceGroupId}:coherence-dependencies:${coherenceVersion}")

    implementation "${coherenceGroupId}:coherence"

    testImplementation "org.junit.jupiter:junit-jupiter-api"
    testImplementation "io.helidon.microprofile.tests:helidon-microprofile-tests-junit5"
    testImplementation "org.hamcrest:hamcrest"
    testImplementation "com.oracle.bedrock.coherence:coherence-12.2.1-testing-support:5.0.20"

    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine")
}

test {
    useJUnitPlatform()
}


task runServer(type: Exec) {
    dependsOn build
    group = "Execution"
    description = "Run a Coherence Cache Server"
    commandLine "java", "-classpath", sourceSets.main.runtimeClasspath.getAsPath(), "-Dcoherence.cacheconfig=topics-cache-config.xml",
            "com.tangosol.net.DefaultCacheServer"
}

task runClient(type: Exec) {
    standardInput = System.in
    dependsOn build
    group = "Execution"
    description = "Run Chat Application"
    def userId = findProperty('userId') ?: "default"
    commandLine "java", "-classpath", sourceSets.main.runtimeClasspath.getAsPath(), 
            "-Dcoherence.cacheconfig=topics-cache-config.xml", "-Xms128m", "-Xmx128m",
            "-Dcoherence.distributed.localstorage=false", "com.oracle.coherence.guides.topics.ChatApplication", userId
}
